apply plugin: "com.android.application"
apply plugin: "jacoco"
apply plugin: "de.mannodermaus.android-junit5"

buildscript {
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:4.1.0"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.6.2.0"
        classpath "org.jacoco:org.jacoco.core:0.8.6"
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation "androidx.constraintlayout:constraintlayout:2.0.2"
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "com.github.stefanbirkner:system-lambda:1.1.1"
    androidTestImplementation "com.google.guava:guava:30.0-android"
    androidTestImplementation "de.mannodermaus.junit5:android-test-core:1.2.0"
    androidTestImplementation "javax.inject:javax.inject:1"
    androidTestImplementation "org.assertj:assertj-core:3.18.0"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-params:5.7.0"
    androidTestImplementation "org.mockito:mockito-android:3.5.15"
    androidTestImplementation "org.mockito:mockito-junit-jupiter:3.5.15"
    androidTestRuntimeOnly "de.mannodermaus.junit5:android-test-runner:1.2.0"
}

android {
    buildToolsVersion "29.0.3"
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    defaultConfig {
        applicationId "org.tinylog.core.android"
        minSdkVersion 26
        targetSdkVersion 30
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument("runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder")
    }

    packagingOptions {
        exclude("META-INF/LICENSE*")
        exclude("META-INF/licenses/*")
    }

    sourceSets {
        main {
            java {
                srcDirs += "../tinylog-core-base/src/main/java"
                srcDirs += "../tinylog-api/src/main/java"
                srcDirs += "../tinylog-impl/src/main/java"
            }

            resources {
                srcDirs += "../tinylog-core-base/src/main/resources"
                srcDirs += "../tinylog-api/src/main/resources"
                srcDirs += "../tinylog-impl/src/main/resources"
            }
        }

        androidTest {
            java {
                srcDirs += "../tinylog-core-base/src/test/java"
                srcDirs += "../tinylog-impl/src/test/java"
            }

            resources {
                srcDirs += "../tinylog-core-base/src/test/resources"
                srcDirs += "../tinylog-impl/src/test/resources"
            }
        }
    }
}
